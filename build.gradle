import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java-library'
}

group "xyz.oribuin"
version "1.0.0-pre1"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url 'https://libraries.minecraft.net' }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.rosewooddev.io/repository/public/" }
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // General Plugin Dependencies
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.jetbrains:annotations:23.0.0'

    // Frameworks & MySQL APIs
    implementation 'dev.rosewood:rosegarden:1.1.0.49-SNAPSHOT'
//    implementation('dev.triumphteam:triumph-gui:3.1.3') {
//        exclude group: 'com.google.code.gson', module: 'gson' // Already included in spigot-api
//        exclude group: "net.kyori", module: "*" // Already included in paper-api
//    }
}

shadowJar {
    archiveClassifier.set(null)
    minimize {
        exclude(dependency("dev.rosewood:rosegarden:.*"))
    }

    relocate "org.jetbrains", "${project.group}.libs.jetbrains"
    relocate "dev.rosewood.rosegarden", "${project.group}.libs.rosegarden"
    relocate "dev.triumphteam.gui", "${project.group}.libs.gui"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}


tasks.getByName("build").dependsOn("shadowJar")